// Copyright (C) 2019 Centrality Investments Limited
// Licensed under the Apache License, Version 2.0 (the "License");
// you may not use this file except in compliance with the License.
// You may obtain a copy of the License at

//     http://www.apache.org/licenses/LICENSE-2.0

// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS,
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and
// limitations under the License.

/* The pact language PEG definition */
contract = _{ SOI ~ input_declaration ~ statement* ~ EOI }
statement = _{ definition | assertion }

// input declaration
input_declaration = { "given parameters" ~ identifier ~ ("," ~ identifier)* }

// conjunctions
and = { "and" }
or = { "or" }
conjunction = _{ or | and }

// Imperatives
must_be = { "must be" }
must_not_be = { "must not be" }
imperative = _{ must_be | must_not_be }

// Comparators
element_of = { "one of" }
eq = { "equal to" }
lt = { "less than" }
lte = { "less than or equal to" }
gt = { "greater than" }
gte = { "greater than or equal to" }
comparator = _{ eq | gte | gt | lte | lt | element_of }

assertion = { subject ~ imperative ~ comparator ~ subject ~ (conjunction ~ assertion)? }
definition = { "define" ~ identifier ~ "as" ~ value }

// Variables
subject = _{ value | identifier }
value = { string | strings | integer | integers }
 // one or more "," delimited `integer`
integers = { list_open ~ integer ~ ("," ~ integer)* ~ list_close }
// one or more ',' delimited `string`
strings = { list_open ~ string ~ ("," ~ string)* ~ list_close }
integer = @{ ASCII_DIGIT+ }
string = { quote ~ ASCII_ALPHANUMERIC+ ~ quote }
identifier = @{ dollar ~ ASCII_ALPHA+ ~ (ASCII_ALPHANUMERIC)* }
dollar = _{ "$" }
quote = _{ "\"" }
list_open = _{ "[" }
list_close = _{ "]" }

WHITESPACE = _{ " " | "\t" | NEWLINE }