// Copyright 2019 Centrality Investments Limited
// This file is part of Pact.
//
// Licensed under the LGPL, Version 3.0 (the "License");
// you may not use this file except in compliance with the License.
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS,
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and
// limitations under the License.

// You should have received a copy of the GNU General Public License
// along with Pact. If not, see:
//   <https://centrality.ai/licenses/gplv3.txt>
//   <https://centrality.ai/licenses/lgplv3.txt>


/* The pact language PEG definition */
contract = _{ SOI ~ input_declaration ~ statement* ~ EOI }
statement = _{ definition | assertion }

// input declaration
input_declaration = { "given parameters" ~ identifier ~ ("," ~ identifier)* }

// conjunctions
and = { "and" }
or = { "or" }
conjunction = _{ or | and }

// Imperatives
must_be = { "must be" }
must_not_be = { "must not be" }
imperative = _{ must_be | must_not_be }

// Comparators
eq = { "equal to" }
lt = { "less than" }
lte = { "less than or equal to" }
gt = { "greater than" }
gte = { "greater than or equal to" }
comparator = _{ eq | gte | gt | lte | lt }

assertion = { subject ~ imperative ~ comparator ~ subject ~ (conjunction ~ assertion)? }
definition = { "define" ~ identifier ~ "as" ~ value }

// Variables
subject = _{ value | identifier }
value = { string | integer }
integer = @{ ASCII_DIGIT+ }
string = { quote ~ ASCII_ALPHANUMERIC+ ~ quote }
identifier = @{ dollar ~ ASCII_ALPHA+ ~ (ASCII_ALPHANUMERIC)* }
dollar = _{ "$" }
quote = _{ "\"" }

WHITESPACE = _{ " " | "\t" | NEWLINE }